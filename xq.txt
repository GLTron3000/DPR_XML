declare option saxon:output "doctype-public=-//W3C//DTD XHTML 1.0 Strict//FR";
declare option saxon:output "doctype-system=http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd";
declare option saxon:output "indent=yes";

declare variable $intervenants := doc("master.xml")//intervenant;
declare variable $unites := doc("master.xml")//unite;
declare variable $parcours := doc("master.xml")//parcour;

declare function local:unites($intervenant as element(intervenant))
as element()*
{
    for $unite in $unites[ref-intervenant/@ref = $intervenant/@id]
    order by $unite/nom

    return  <li>{data($unite/nom)}</li>
};

declare function local:parcours($intervenant as element(intervenant))
as element()*
{
    for $parcour in $parcours[$unites[ref-intervenant/@ref = $intervenant/@id]/ref-parcour/@ref = @id]
    order by $parcour/nom-court

    return <li>{data($parcour/nom-court)}</li>
};

<html>
<head>
  <meta charset="utf-8"/>
  <meta content="width=device-width, initial-scale=1"
        name="viewport"/>
  <title>XQuery</title>
  <link href="style.css"
        rel="stylesheet"/>
  <link href="favicon.ico"
        rel="icon"
        type="image/x-icon"/>
</head>
<body>
    <h1>Liste des enseignants</h1>
    <ul>
    {
        for $intervenant in $intervenants
        order by $intervenant/nom

        return <li class="intervenant">
            <h2>{data($intervenant/prenom)}&#160;{data($intervenant/nom)}</h2>
            <p>Email: {data($intervenant/email)}</p>
            <h3>Unit√©s</h3>
            <ul class="sousliste"> 
                {local:unites($intervenant)} 
                &#160;
            </ul> 

            <h3>Parcours</h3>
            <ul class="sousliste"> 
                {local:parcours($intervenant)} 
                &#160;
            </ul> 
            
        </li>
    }
    </ul> 
</body>
</html>